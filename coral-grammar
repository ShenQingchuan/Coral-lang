root ::= (statement)*

statement
  ::= simpleStmt
    | breakStmt
    | continueStmt
    | returnStmt
    | packageStmt
    | importStmt
    | enumStmt
    | blockStmt
    | tryCatchStmt
    | ifStmt
    | switchStmt
    | whileStmt
    | forStmt
    | eachStmt
    | functionDeclaration
    | classDeclaration

packageStmt ::= package IDENTIFIER ';'

/* importStmt Example:
    from httplib import {
        Request  as HttpReq;
        Response as HttpRes;
    }
*/
moduleName ::= IDENTIFIER
importElement ::= moduleName ('as' IDENTIFIER)?
singleGlobalImport ::= 'import' importElement ';'
importFrom ::= 'from' moduleName 'import' (importElement ';' | ('{' importElement (',' importElement)* '}'))
importStmt ::= singleGlobalImport | importFrom

/* enumStmt Example:
    enum Sex {
        FEMALE = 0,
        MALE,
        SECRET
    }
*/
enumElement ::= IDENTIFIER ('=' decimalLit)?
enumStmt ::= 'enum' IDENTIFIER '{' enumElement (',' enumElement)* '}'

nilLit ::= 'nil'
decimalLit ::= [0-9]+
octalLit ::= '0o' [0-7]+
hexadecimalLit ::= '0x' [0-9a-fA-F]+
binaryLit ::= '0b' [01]+
floatLit ::= [0-9]+ '.' [0-9]+
exponentLit ::= [0-9]+ ('.' [0-9]+)? 'e' ('+' | '-')? [0-9]+
unicodeDigits ::= '\\' ('u'|'U') [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
hexBytesDigits ::= '\\' 'x' [0-9a-fA-F] [0-9a-fA-F]
escapeValue ::= (unicodeDigits | hexBytesDigits | '\\' ['"abfnrtv\\])
charLit ::= '\'' (~[\n\\] | escapeValue) '\''
stringLit ::= '"' (~["\\] | escapeValue)*  '"'
arrayLit ::= '[' expressionList? ']'
tableElement ::= IDENTIFIER ':' expression
tableLit ::= '{' tableElement (',' tableElement)* '}'
lambdaLit ::= signature '->' (blockStmt | expression)
literal
    ::= nilLit
    | trueLit
    | falseLit
    | decimalLit
    | octalLit
    | hexadecimalLit
    | binaryLit
    | floatLit
    | exponentLit
    | charLit
    | stringLit
    | arrayLit
    | tableLit
    | LambdaLit
    | thisLit
    | superLit
operandName ::= IDENTIFIER
operand ::= literal | operandName
index ::= '[' expression ']'
slice ::= '[' expression? ':' expression? ']'
call ::= '(' expressionList? ')'
member ::= expression '.' IDENTIFIER ('.' IDENTIFIER)*
primaryExpr ::= operand (index | slice | call ï½œ member)?
newInstanceExpression ::= 'new' typeDescription '(' expressionList ')'
unaryExpr ::= ('-' | '!' | '~') expression
binaryExpr ::= expression <binaryOperator> expression
expression
    ::= '(' expression ')'
    | primaryExpr
    | newInstanceExpression
    | unaryExpr
    | binaryExpr
expressionList ::= expression (',' expression)*
primaryExpressionList ::= primaryExpr (',' primaryExpr)*

incDecStmt ::= expression ('++' | '--') ';'
breakStmt ::= 'break' ';'
continueStmt ::= 'continue' ';'
returnStmt ::= 'return' expressionList? ';'
mixAssignOperator ::= ('+' | '-' | '|' | '^' | '*' | '/' | '%' | '<<' | '>>' | '&') '='
assignStmt ::= (primaryExpressionList '=' expressionList) ';'
typeName ::= IDENTIFIER
typeDescription ::= (typeDescription '[' ']') | (typeName ('<' typeName (',' typeName)* '>')? )
variableDeclElement ::= IDENTIFIER (typeDescription | (typeDescription? '=' expression))
variableDeclStmt ::= ('var' | 'val') variableDeclElement (',' variableDeclElement)* ';'
simpleStmt
    ::= expression ';'
    | incDecStmt
    | variableDeclStmt
    | assignStmt

blockStmt ::= '{' statement* '}'

/* ifStmt Example:
    if p.sex == FEMALE && p.age < 30 {
        println("Welcome, young lady.");
    } elif p.sex == MALE {
        println("Hello, sir.");
    }
*/
ifStmt ::= 'if' expression blockStmt ('elif' expression blockStmt)* ('else' blockStmt)?

/* switchStmt Example:
    switch tom.grade {
        default {
            throw new Exception("Incorrect grade value!");
        }
        case 0...59 {
            println("Failed to pass this exam.");
        }
        case 60...70 {
            println("Lucky pass!");
        }
        case 71...80 {
            println("Not bad.");
        }
        case 81...99 {
            println("Excellent!");
        }
        case 100 {
          println("Full Mark!");
        }
    }
*/
normalCase ::= expressionList
rangeCase ::= expression '...' expression
caseBlock ::= (singleCase | rangeCase) blockStmt
switchStmt ::= 'switch' expression '{' ('default' blockStmt)? caseBlock* '}'

/* whileStmt Example:
    while p {
        print(p.value);
        if p.next {
            print("->");
        }
        p = p.next;
    }
*/
whileStmt ::= 'while' expression blockStmt

/* forStmt Example:
    for var i = 0, j = arr.length; i <= j; i++, j-- {
      println(i + j);
    }
*/
forStmt ::= 'for' simpleStmt? ';' expression ';' (simpleStmt (',' simpleStmt)* )? blockStmt

/* eachStmt Example:
    each e in [2,4,9,11,18] {
        println(e);
    }
*/
eachStmt ::= 'each' IDENTIFIER (',' IDENTIFIER)? 'in' expression blockStmt

/* functionStmt Example:
    fn fibonacci<T>(n T) int {
        var a = n % 2, b = 1;
        for var i = 0; i < n/2; i++ {
            a += b;
            b += a;
        }

        return a;
    }
*/
argument ::= IDENTIFIER typeDescription
argumentList ::= argument (',' argument)*
resultList ::= typeDescription (',' typeDescription)*
signature ::= '(' argumentList* ')' resultList? ('throws' typeDescriptor)?
functionDeclaration ::= 'fn' IDENTIFIER genericsArgs signature blockStmt

/* classStmt Example:
    class VideoDisk<T, K> : Disk<K> <- Playable<T> {
        val time T;
        var movieName string;
        var movieDirector string;
        var productionCompany string;

        var hasBeenRented bool = false;
        var renter Customer;

        public fn rent(c Customer) {
            this.renter = Customer;
            this.hasBeenRented = true;
        }
    }
*/
scopeKeyword ::= 'public' | 'private'
genericsArgElement ::= IDENTIFIER (genericsArgs)?
genericsArgs ::= '<' genericsArgElement (',' genericsArgElement)* '>'
classIdentifier ::= IDENTIFIER (genericsArgs)?
classMemberVariable ::= scopeKeyword? variableDeclStmt
classMemberMethod ::= scopeKeyword? functionDeclaration
classDeclaration ::= 'class' classIdentifier (':' classIdentifier)? ('<-' classIdentifier (',' classIdentifier)* )
  '{' (classMemberVariable | classMemberMethod)* '}'

/* interface RunnableSon<T> : RunnableParent<T> {
      public fn run();
   }
*/
interfaceMethodDecl ::= scopeKeyword? 'fn' IDENTIFIER genericsArgs signature ';'
interfaceDeclaration ::= 'interface' classIdentifier (':' classIdentifier)? '{' interfaceMethodDecl+ '}'

/* tryCatchStmt Example:
    try {
        val n = 3 / 0;
    } catch e MathException {
        println(e.stacktrace());
    } finally {
        println("hahaha, it's ok");
    }
*/

catchHandler ::= ('catch' IDENTIFIER typeDescriptor blockStmt)
tryCatchStmt ::= 'try' blockStmt catchHandler+ ('finally' blockStmt)?
